;;; -*- Mode: Lisp; Syntax: Common-Lisp -*-
;;; Code from Paradigms of AI Programming
;;; Copyright (c) 1991 Peter Norvig

;;; File auxfns.lisp: Auxiliary functions used by all other programs
;;; Load this file before running any other programs.
;;(in-package "USER")
;;;; Implementation-Specific Details

;; #+:COMMON-LISP 

(eval-when (eval compile load)
  ;; Make it ok to place a function definition on a built-in LISP symbol.
  #+(or Allegro EXCL)
  (dolist (pkg '(excl common-lisp common-lisp-user))
    (setf (excl:package-definition-lock (find-package pkg)) nil))

  ;; Don't warn if a function is defined in multiple files --
  ;; this happens often since we refine several programs.
  #+Lispworks
  (setq *PACKAGES-FOR-WARN-ON-REDEFINITION* nil)

  #+LCL 
   (compiler-options :warnings nil)
  )

;;;; REQUIRES

;;; The function REQUIRES is used in subsequent files to state dependencies
;;; between files.  The current definition just loads the required files,
;;; assumming they match the pathname specified in *PAIP-DIRECTORY*.
;;; You should change that to match where you have stored the files.
;;; A more sophisticated REQUIRES would only load it if it has not yet
;;; been loaded, and would search in different directories if needed.

(defun requires (&rest files)
  "The arguments are files that are required to run an application."
  (mapc #'load-paip-file files))

(defvar *paip-files*
  `("auxfns" "tutor" "examples" 
    "intro" "simple" "overview" "gps1" "gps" "eliza1" "eliza" "patmatch" 
    "eliza-pm" "search" "gps-srch" "student" "macsyma" "macsymar" "unify" 
    "prolog1" "prolog" "prologc1" "prologc2" "prologc" "prologcp" 
    "clos" "krep1" "krep2" "krep" "cmacsyma" "mycin" "mycin-r" "waltz" 
    "othello" "othello2" "syntax1" "syntax2" "syntax3" "unifgram" 
    "grammar" "lexicon" "interp1" "interp2" "interp3" 
    "compile1" "compile2" "compile3" "compopt"))

(defparameter *paip-directory*
  (make-pathname :name nil :type nil
		 :defaults (or (and (boundp '*load-truename*) *load-truename*)
			       (truename ""))) ;;??? Maybe Change this
  "The location of the source files for this book.  If things don't work,
  change it to reflect the location of the files on your computer.")

(defparameter *paip-source* 
  (make-pathname :name nil :type "lisp" ;;???  Maybe Change this
		 :defaults *paip-directory*)) 

(defparameter *paip-binary*
  (make-pathname
   :name nil
   :type (first (list #+LCL (first *load-binary-pathname-types*)
		      #+Lispworks system::*binary-file-type*
		      #+MCL "fasl"
		      #+Allegro excl:*fasl-default-type*
		      #+(or AKCL KCL) "o"
		      #+CMU "sparcf"
		      #+CLISP "fas"
		      "bin"))  ;;???  Maybe Change this
   :directory (append (pathname-directory *paip-source*) '("bin"))
   :defaults *paip-directory*))

(defun paip-pathname (name &optional (type :lisp))
  (make-pathname :name name 
		 :defaults (ecase type
			     ((:lisp :source) *paip-source*)
			     ((:binary :bin) *paip-binary*))))

(defun compile-all-paip-files ()
  (mapc #'compile-paip-file *paip-files*))

(defun compile-paip-file (name)
  (let ((path (paip-pathname name :lisp)))
    (load path)
    (compile-file path :output-file (paip-pathname name :binary))))

(defun load-paip-file (file)
  "Load the binary file if it exists and is newer, else load the source."
  (let* ((src (paip-pathname file :lisp))
	 (src-date (file-write-date src))
	 (bin (paip-pathname file :binary))
	 (bin-date (file-write-date bin)))
    (load (if (and (probe-file bin) src-date bin-date (>= bin-date src-date))
	      bin
	    src))))


;;;; Macros (formerly in auxmacs.lisp: that file no longer needed)
;;(in-package :LISP)

(eval-when (load eval compile)
  (defmacro once-only (variables &rest body)
    "Returns the code built by BODY.  If any of VARIABLES
  might have side effects, they are evaluated once and stored
  in temporary variables that are then passed to BODY."
    (assert (every #'symbolp variables))
    (let ((temps nil))
      (dotimes (i (length variables)) (push (gensym) temps))
      `(if (every #'side-effect-free? (list .,variables))
	(progn .,body)
	(list 'let
	 ,`(list ,@(mapcar #'(lambda (tmp var)
			       `(list ',tmp ,var))
			   temps variables))
	 (let ,(mapcar #'(lambda (var tmp) `(,var ',tmp))
		       variables temps)
	   .,body)))))

  (defun side-effect-free? (exp)
    "Is exp a constant, variable, or function,
  or of the form (THE type x) where x is side-effect-free?"
    (or (atom exp) (constantp exp)
	(starts-with exp 'function)
	(and (starts-with exp 'the)
	     (side-effect-free? (third exp)))))

  (defmacro funcall-if (fn arg)
    (once-only (fn)
	       `(if ,fn (funcall ,fn ,arg) ,arg)))

  (defmacro read-time-case (first-case &rest other-cases)
    "Do the first case, where normally cases are
  specified with #+ or possibly #- marks."
    (declare (ignore other-cases))
    first-case)

  (defun rest2 (x)
    "The rest of a list after the first TWO elements."
    (rest (rest x)))

  (defun find-anywhere (item tree)
    "Does item occur anywhere in tree?"
    (if (atom tree)
	(if (eql item tree) tree)
	(or (find-anywhere item (first tree))
	    (find-anywhere item (rest tree)))))

  (defun starts-with (list x)
    "Is x a list whose first element is x?"
    (and (consp list) (eql (first list) x)))
  )

;;;; Auxiliary Functions

(setf (symbol-function 'find-all-if) #'remove-if-not)

(defun find-all (item sequence &rest keyword-args
                 &key (test #'eql) test-not &allow-other-keys)
  "Find all those elements of sequence that match item,
  according to the keywords.  Doesn't alter sequence."
  (if test-not
      (apply #'remove item sequence 
             :test-not (complement test-not) keyword-args)
      (apply #'remove item sequence
             :test (complement test) keyword-args)))

(defun partition-if (pred list)
  "Return 2 values: elements of list that satisfy pred,
  and elements that don't."
  (let ((yes-list nil)
        (no-list nil))
    (dolist (item list)
      (if (funcall pred item)
          (push item yes-list)
          (push item no-list)))
    (values (nreverse yes-list) (nreverse no-list))))

(defun maybe-add (op exps &optional if-nil)
  "For example, (maybe-add 'and exps t) returns
  t if exps is nil, exps if there is only one,
  and (and exp1 exp2...) if there are several exps."
  (cond ((null exps) if-nil)
        ((length=1 exps) (first exps))
        (t (cons op exps))))

;;; ==============================

(defun seq-ref (seq index)
  "Return code that indexes into a sequence, using
  the pop-lists/aref-vectors strategy."
  `(if (listp ,seq)
       (prog1 (first ,seq)
              (setq ,seq (the list (rest ,seq))))
       (aref ,seq ,index)))

(defun maybe-set-fill-pointer (array new-length)
  "If this is an array with a fill pointer, set it to
  new-length, if that is longer than the current length."
  (if (and (arrayp array)
           (array-has-fill-pointer-p array))
      (setf (fill-pointer array) 
            (max (fill-pointer array) new-length))))

;;; ==============================

;;; NOTE: In ANSI Common Lisp, the effects of adding a definition (or most
;;; anything else) to a symbol in the common-lisp package is undefined.
;;; Therefore, it would be best to rename the function SYMBOL to something 
;;; else.  This has not been done (for compatibility with the book).  

(defun symbol (&rest args)
  "Concatenate symbols or strings to form an interned symbol"
  (intern (format nil "狺狎珞┅ㄤ彐躅铄鳝簌礅镬é蝈篝狎珞⒚镱汜翦钺翦簌礅镬矧篝蜷铉麸骘蝽犷躅轭翦蝾邃簌礅镬磲脲簌礅镬ㄦ矧磲铋狺狎珞┅ㄤ彐躅灬篝扉篝⒁弭躜翳灬篝屐屙孱铒灬篝泔铙沐祆镦扉篝ㄦ轵篝灬篝扉篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅磲痧孱ㄦ扉篝⒘痧孱翳蝈篚祠镦汜祆轭骖镱遽汨屐屙孱镦扉篝涕脲磲疸镱怩躞弩狃疱钿轭篝遽镦钽镱惝ㄡ痧禊＇狃疱钿磲疸狎骖扉篝┅ㄤ彐躅黼扉篝⑸轶扉篝蝈趱蝾轸雉桢蝼轶蝈趱蝾翳扉篝镦ㄩ扉篝扉篝┅ㄤ彐躅骒狒翦ㄥ皓⑶弭蜷镦轫忮滗邃扉篝麸镱戾鲥镱禊┊磲痧孱＇黼扉篝屮皓ㄤ彐躅蜥钿镯屐箦瘵⑿殂蜥钿镯屐屙孱秕镦箦聃孱沐ㄥ祠箦蜥钿镯戾铉翳箦瘵┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅礤礅弪羼踽ㄩ翦扉篝礤礅弪轸屙扉篝呼弩＇羼踽飑换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅泔眇矬é蝈篝骢钽糸镱螬＇灬礅溽蝈漉沐＇骢钽犰骢钽糸镱烘蝻憝孱洪铋糸犰鲠祯┅换换澡腻怩珑轭硝麴豸漆汩扉豉ㄤ彐鲠溻绛殇螵铋⑸溴铘殒殄蝮躞邃怡溻纰ㄤ彐躅溻ㄩ骘蝽狒篝蜷铉蝈篝狎珞⑿蜷铘溴怩珑轭轭骘殒呐抡赡栳忮孱箴邈殒殄洚麒孱礤礅弪殇溻绛殇螵ㄦ蝈箬扉铄溴怩绛轱ㄡ痧禊＇骘蝽狒溴怩绛轱骘蝽狒篝蜷铉狎珞┅ㄤ彐躅溴怩é蝈篝殇螬⒂翎螋溻秕麴豸镱翳玳鲥殇螽箦翩溻绛殇螵躅轱殇溻绛殇螵┅ㄤ彐躅躅溴怩é蝈篝殇螬⒂麸溻镱翳殇螽组翳铒殇蟋篝镳溻犰麸珏翳弪箦翩溻绛殇螵ㄩ铛祆殇螬铋箦舡溟骀弪孱沐溻绛殇螵殇螬┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅溻绛轭溴铘ㄩ轭溴铘骘蝽狒篝蜷铉蝈篝狎珞⑿蜷铘轭溴铘邃溴怩珑轭轭骘殒呐抡赡栳忮孱箴邈殒殄洚麒孱礤礅弪殇溻绛殇螵ㄦ蝈箬扉铄溴怩绛轱ㄤ雉轫弩ㄩ轭溴铘痱轭溴怩绛轱┅ㄡ痧禊＇骘蝽狒溴怩绛轱骘蝽狒篝蜷铉狎珞┅换换辛栽乓土悦壬吻屏蒙躺再ㄤ彐泔铙翎铘驷殪铋飑ㄤ彐泔铙翎铘铒忾钿轭珞Ж舂┅ㄤ彐躅疳舡磲翥疳趑弪轭瘐镳糸镱犰ㄢ轭溟铉铒忾钿轭珞┅⑼狒汨疳趑弪徵衢铙轭瘐轭翳泔铘屮镦翳忾钿轭珞ㄣ镱è羼忾钿轭珞驷殪驷殪è鲠蜷徕戾疳趑弪瞟磲翥璀鲠蜷徕戾疳趑弪轭瘐忾钿轭珞┅è羼疳趑弪轭瘐舂忾钿轭珞è犷ㄣ镱箴疳趑弪瞟ㄣ镱箴轭瘐舂疳舡磲翥蝈篝疳趑弪瞟蝈篝轭瘐舂疳舡磲翥ㄦ轵篝疳趑弪瞟ㄦ轵篝轭瘐舂忾钿轭珞┅驷殪┅ㄤ彐躅磲翥璀鲠蜷徕戾鲠轭瘐忾钿轭珞⒛镥至磲翥轭瘐艨阵弩矧躔溽翦螬犷蝈趱蝾忾钿轭珞戾è忾钿轭ㄧ弭忾钿轭鲠忾钿轭珞┅ㄣ镱è铒忾钿轭绌ㄥ翦钿忾钿轭珞鲠轭瘐忾钿轭珞┅è羼踽轭瘐ㄢ轭溟铉鲠忾钿轭绌忾钿轭珞驷殪┅┅ㄤ彐躅磲脲忾钿轭鲠鲠飑ㄣ镱鲠鲠飑ㄤ彐躅忾钿轭绛鲠ㄢ轭溟铉⑶弭翳鲠蜷徕戾疳螋镦箝铉戾忾钿轭绠ㄣ狎忾钿轭绌ㄤ彐躅忾钿轭绛鲠ㄢ轭溟铉⑶弭翳鲠祯疳螋镦箝铉戾忾钿轭绠ㄣ潋忾钿轭绌ㄤ彐躅珏舡忾钿轭鲠忾钿轭珞⑵轭鲠蜷徕戾鲠祯濠疳轵轭忾钿轭扉篝ㄡ篌镢鲠忾钿轭珞┅ㄤ彐躅祜镫躔鲠忾钿轭珞⑶弭翳鲠祯疳螋ㄦ矧鲠颟骝镯忾钿轭扉篝ㄢ轭溟铉鲠ㄧ弭忾钿轭鲠忾钿轭珞┅ㄤ彐躅屮翦钿忾钿轭珞鲠鲠忾钿轭珞⒘滗鲠鲠祯濠疳轵麸忾钿轭扉篝ㄣ镱ㄣ镱鲠鲠飑换项沐麇徜Ⅱ遽膦忾钿轭绗换麇汜珏蜷镦翳漉眄铒忾钿轭珞ㄩㄥ忾钿轭珞铒忾钿轭珞铋忾钿轭珞┅ㄤ彐躅鲠蜷徕戾⑸鲠蜷徕戾ㄡ簌礅镬忮玳铑轭鏖翳嗫З竣ㄡ钿簌礅镬ㄥ聃犰ㄥ祠簌礅镬钺礤癌＼咯┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇换换澡湾盹辁狒轱驷汩扉豉ㄤ彐磲泸溴骢瞽礤盹ㄦ狎珞怙澌怙澌⒛彐轭礤盹辁邃骢钽糸镱啜礤盹辁ㄤ彐躅骖狎珞怙澌┅ㄤ彐躅礤盹ㄦ脲脲＇骈蝮舂翦篝＇羼飑钺礤⒁弭躜礤盹骢钽糸镱镦骖戾è翎忪磲脲栳箬翎忪呼弩翦篝┅箦翩ㄧ弭钺礤ы屙铹翎忪濠＇灬礅溽é蝈篝狎珞戾èㄦ躅汜祆脲狎珞┅眭祠轲戾鲠祯瀛忾钿鲠骘躅洵皓ㄧ弭栳箬翎忪濠ㄩ骘躅洵鲠箦翩ㄧ弭栳箬翎忪濠ㄡ痧禊骖狎珞┅┅┅┅ㄤ彐躅礤盹辁ㄦ瞽钺礤脲脲＇骈蝮舂翦篝＇羼飑⒁屦灬沐骖钺礤珈镡犰溴骈铋糸镱鏖翳礤盹辁邃鲥蝮轱町ㄣ戾狎礤盹辁骖钺礤箦翩簌礅镬骢钽糸镱骖钺礤礤盹簌礅镬骢钽糸镱骖钺礤侯犴骖钺礤弘妁脲呼弩翦篝┅ㄤ彐躅沆遽颦礤盹辁ㄦ瞽钺礤⒚戾狎翳栳箬翎忪骝镯礤盹骢钽糸镱戾è翎忪ㄧ弭骖钺礤ы屙铹┅麒孱翎忪ㄣ祢栳箬翎忪濠┅换换腻灬邃泔眇豸狒轱詈ㄤ彐篝蝓泗溴灬鲠祯ㄣ镯瘐翦淇铋飑ㄤ彐磲泸溴灬é蝈篝怙澌⒘泔眇豸狒轱翳狒汜忮屮邈豸邃灬翦怡葡颐女啜磲脲溴灬忽犰蹂＇灬礅溽ī怙澌┅ㄤ彐躅骘蜚ㄤ屐狴⒛溴灬邃泔眇豸狒轱瞵矧驽翥轸痱弼轱躞禊泔眇豸邃鲠祯瀹ㄩㄤ屐狴泔眇豸邃溴灬ㄤ屐狴鲠祯溴灬痱镧箦翩ㄤ屐狴鲠祯溴灬ㄦ躅汜祆ㄤ屐狴鲠祯溴灬┅箦翩ㄤ屐狴泔眇豸邃溴灬舂┅换换腻骝弩秕蜚搴ㄤ彐磲泸溴骝弩秕蜚钺礤脲泔铙趄蹉麸ㄩ铋糸犰泔痖弩癌箝磲轭轸獒飙泔痖弩卑┅戾è蝈箫躜沐簌礅镬И簌礅镬钺礤Л蝈箫躜沐┅ㄤ遽祆镢狒簌礅镬т遽祆镢狒瀛钺礤┅ㄡ祆镢狒簌礅镬п祆镢狒瀛钺礤┅啜痱镧ㄤ彐疳蜥礤翦蝈箫躜沐磲脲狎蜥箝烘殪飙痫轭翦癌ㄤ彐躅犰祜汜翦ī⑶弭犷屐屙孱骝镯翳蝈箫躜沐痫镬矧磲脲镱瀹ㄩㄦ殪飙痫轭翦蝈箫躜沐癌泔铙趄蹉麸鲥泗矧痫蝈箫躜沐┅ㄤ彐躅溴犰祜汜翦ì钺礤⑿灬沐铒祜铉弪铄邃邃屐屙孱忉汶轭翳痫镬鲥泗矧瘐箬屮翦钿钺礤蝈箫躜沐┅ㄩ轭轸獒飙泔痖弩癌啜磲疸＇溴犰祜汜翦祜镳蝈疱狒轭轸獒飙泔痖弩泔祆邈ì犰祜汜翦┅┅К钺礤┅ㄤ彐磲泸鏖翳蝈箫躜沐è鲠蝈箫躜沐镳糸镱犰痱雉邈舂蝈篝怙澌⑴邈豸怙澌鏖翳至怙躅麸犷轭篝犷沐镦遗酉找门戾è犰祜汜翦簌礅镬п祆镢狒瀛蝈箫躜沐┅ㄤ遽祆镢狒簌礅镬т遽祆镢狒瀛蝈箫躜沐┅ㄩ痱雉邈啜戾è鲠铋飑躅鏖钿痱雉邈痱镧箦翩鲠ì犰祜汜翦┅棱镤躅戾篌铛祆鲠颟ì溴犰祜汜翦鲠颟┅啜戾è鲠ì犰祜汜翦┅棱镤ì溴犰祜汜翦鲠颟┅┅换换氧艴弩换聃艴轶灬篝泔铘孱趔疳轵ㄤ彐躅聃艴瀛泔铘孱趔瘵ㄣ潋瘵ㄤ彐躅磲脲聃艴ī⒙蹰熹铄聃艴瀣鏖翳铒屐屙孱趔戾èㄣ镱铋铋飑┅箦翩ㄣ狎瘵瘵┅ㄤ彐躅孱聃艴ㄩ翦瘵⑸铙弪轸屙狒翳孱镦翳聃艴瀹箦翩ㄣ狎瘵箦翩蝈篝ㄣ狎瘵ㄣ镱轸屙铋飑┅瘵ㄤ彐躅溴聃艴瘵⒁屙秭犷轸屙骝镯翳骝镱镦翳聃艴瀹痫ㄣ潋瘵ㄩ铛祆ㄣ潋瘵箦翩ㄣ狎瘵瘵瘵ㄤ彐躅骝镱瘵ㄦ轵篝聃艴瀛泔铘孱趔瘵┅ㄤ彐躅屙痿聃艴瀛瘵铛祆聃艴瀛泔铘孱趔瘵┅ㄤ彐躅聃艴瀛钽镱扉篝⒘滗翳屐屙孱趔镦躺釉麸翳孱镦翳聃艴瀹箦翩ㄣ狎瘵灬篝箦翩蝈篝ㄣ狎瘵扉篝┅┅换换萧桢蚝ㄤ彐躅箫螋箦痱邃脲脲⒂矧鏖翳秕犰翦蜷铉翳箦聃孱沐箫螋ㄣ镳箦箦瘵痱邃弘妁脲┅ㄤ彐躅蝈躞瀛泔铙⒁弭躜ㄣ镱┈矧蝈躞殒轸轶羼踽麸ㄣ镱ㄩㄡ钿ㄥ耢ㄣ狎┅ㄥ耢ㄣ潋┅ㄣ镱┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅戾铉翳奖⑸扉篝镦戾铉翳笨ㄡ钿ㄣ镱箴铛祆ㄣ潋┅┅ㄤ彐躅蝈篝扉篝⒃桢蝈篝镦扉篝徭翦翳骈蝮匀遗屐屙孱趔ㄣ滗潋扉篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅躅轳蹂骈钿殒犷麒弪痱邃殂狒趄邋镳糸镱犰骘躅洵箫驷颟⒁弭躜扉篝镦戾狯弩镦趄邋筢糸箧轭痱邃殂狒瀣鏖翳漉痨殂狒弩蝈盹鲥洚ㄩㄡ麸趄邋ㄩㄦ躅汜祆痱邃殂狒趄邋ㄡ潢镩趄邋骘躅洵箫驷颟骘躅洵箫驷颟躅轳蹂骈钿殒犷麒弪痱邃殂狒ㄦ轵篝趄邋躅轳蹂骈钿殒犷麒弪痱邃殂狒蝈篝趄邋骘躅洵箫驷颟┅ㄤ彐躅骈钿殒犷麒弪痱邃殂狒趄邋⒛镥痱邃殂狒狃痨麸犷狒镯轭翳趄邋竣ㄩㄡ麸趄邋ㄦ躅汜祆痱邃殂狒趄邋矧ㄦ轭洵殒犷麒弪痱邃殂狒ㄦ轵篝趄邋┅ㄦ轭洵殒犷麒弪痱邃殂狒蝈篝趄邋┅┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐磲泸溴骈铄孱蹴弪狒邃豉疱豉疱蝈篝屐屙孱趔⒁屦蝈箦铘犷孱蹴弪狒邃豉疱鏖翳轭翦珏蝮碍町啜痱镧ㄤ彐豉疱豉疱īЖ轭翦珏ō戾铉翳屐屙孱趔暴┅ㄤ彐躅簌礅镬豉疱Л倔礅镬ì豉疱ㄥ祠К屐屙孱趔豉疱┅ㄤ彐躅簌礅镬簌礅镬豉疱簌礅镬痫箝糸镱簌礅镬К屐屙孱趔┅括祜镳骘屐屙孱轭屐屙孱趔骘骝镯泔祆邈啜溴驺镱篝犷屐屙孱椹┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅铒舡铛祆铒铛祆┅ㄤ彐躅骈蝮舡矧铋⒃桢骈蝮屐屙孱镦殒轸轶扉篝屐箦铋飚ㄩㄣ镱箴ㄦ轵篝铋飑ㄤ彐躅骈蝮舡矧箦戽⒃桢骈蝮屐屙孱镦殒轸轶扉篝屐箦轸箦戽ㄩㄣ镱箴ㄦ轵篝┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇换换锰籼犷廖由锰蔑眇狒殁殪轸躅戾篌ㄦ怙躅漯т彐礤翳镤ㄤ彐磲泸溴骓弭栾钺礤狎珞蝈篝怙澌啜溴骢К钺礤К狎珞棱镤┅躅戾篌ㄦ怙躅漯ы狃轭麸ㄤ彐躅磲瓠轭麸蝈篚祠箦聃孱沐骢钽糸镱蝈篝箦聃孱沐螬⒛弩趄蹉糸鲥禊箦屐屙孱趔镦遗诱淘优颜盼门麸翳蝈篚祠镦狃痨轭普蚊陨衔麸蝈箴邈糸鲥屐屙孱趔镦优颜盼门赢戾è狎珈轶磲脲扉篝戾铉翳箦聃孱沐螬┅ㄩ扉篝蝈篚祠箦聃孱沐盹篝痫箝糸鲥骈铛ㄡ蝌狴溟礤铙轱蝈篚祠箦聃孱沐癌┅换狎珈轶轶磲溴轭麸扉篝镦狎珞骘遽汨汜祆换轶翳戾铉翳镦翳祜铉弩鲥泗矧麒孱箦聃孱沐箦翩黹祜镳骘箦轭箦聃孱沐黹铋黹戾铉翳箦瘵┅┅换腻骈铄箫礤箬狎邃骢钽糸镱蠛ㄦ戾è滹镱瀛汜祆ㄩ祜镳骘箦镱箦聃孱沐骘狎镱狎珈轶滹ㄩ扉篝ㄦ轵篝箦瘵箦翩ㄦ轵篝狎绌痫ㄦ轵篝箦瘵┅箦翩ㄦ轵篝狎绌ㄡ蝈ㄦ轵篝箦瘵椹┅ㄡ痧禊骢钽糸镱狎珈轶舂ㄤ锃蝈篚祠ㄩㄩㄡ钿鲥泗矧蝈篚祠箦聃孱沐ㄡ蝌狴栳蟓骈祆痫轭翦颦蝈篚祠箦聃孱沐┅箦翩ㄦ殪飙痫轭翦蝈篚祠箦聃孱沐磲ㄦ殪飙痫轭翦蝈篚祠箦聃孱沐┅┅┅ㄤ邈灬蝈ㄩ铎轭滹镱瀛汜祆┅换腻汩溴殒翳蝈篚祠轶扉篝矧鲥泗矧换犷祜镳翳蝻蹒遽汨屐屙孱ㄩ扉篝蝈篚祠箦聃孱沐祜镳骘骝镯麸ō暴骘镱蝈篚祠箦聃孱沐滹箦翩ㄦ轵篝颟ㄤ锃镱瀛汜祆椹骈钺祆ㄤ锃蝈篚祠椹祜镳骘骝镯麸ō暴滹箦翩ㄡ蝈蝈篚祠箦聃孱沐椹ㄤ锃镱瀛汜祆椹骈钺祆ㄤ锃蝈篚祠椹┅蝈篚祠箦聃孱沐┅躅戾篌ㄦ怙躅漯с镯痨屙孱舂ㄤ彐躅泔眇戾礤铘ㄦ瞟⑸莆蝈趱蝾翳孱ㄣ镯痨屙孱莆蝈趱蝾铒┊＇灬礅溽é蝈篝狎珞铒ㄡ痧禊骖狎珞┅┅躅戾篌ㄦ怙躅漯鏖翳泔眇殪狒轱瞽躅轸ㄤ彐磲泸鏖翳泔眇殪狒轱瞽躅轸镳糸镱怙澌怙澌⒛翳怙澌怩溴灬泔眇殪弪麽蝾轭珞躅糸翳孱洚换澡狒麽躅溴骈铄骢钽糸镱麽蝾轭珞翳狒狎蝈犰禊换牾篝骘蝼狎蝈驽蝈钽弩鏖祆铒忮痱轭翦狒犰飚换澡轶轶溴骈铄轭蔑眄镱涕箴翳提铉踽珏差邃ㄤ邈灬蝈ㄩ珙矧镳糸镱螬啜蝈徜糸礤汜箦＋涕箴с镯痖戾蚝泔眇殪弪麽蝾轭珞泔铘屮舡忾钿＋条汩鏖翳溴驽蝌邃麽蝾轭珞ю蝻珙怙澌┅换换义漉沐麒孱铋换描犷珏翳轶麸殒秕铄邃遗恼门鏖翳弘妁脲黠蜾ㄤ彐躅蝈漉沐ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓ㄦ躅汜祆ㄩ扉篝箦瘵＇蝈漉沐扉篝＇蝈漉沐鲥泗骖箦骝镯孱矧篝狎癌孱脲轭轸轭轸皓ㄤ彐躅蝈漉沐ㄦ躅泗轱箦聃孱沐脲骝镯孱篝狎孱脲ㄩ铋糸犰鲠祯铋轭轸獒飙鲠祯瀛皓蝈漉沐骢钽糸镱箦聃孱沐骝镯孱篝狎孱脲轭轸獒飙鲠祯轭轸獒飙鲠祯瀛皓ㄤ彐躅蝈漉沐鲥泗ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓ㄩ铛祆孱洎箦翩孱戾铉翳箦瘵┅ㄡ篌弪冀篝狎孱戾铉翳箦瘵篝狎孱洎⑸祆彗犰篚怏羼蹂钽镦后翎螋哄钿洧箦篝狎孱洎ㄣ狍ō孱篝狎舂ūㄩ轭轸ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅òㄩ轭轸轭轸ㄦ躅汜祆骖┅ㄩ铒骝镯孱洎戾è蝈篚祠ㄩ轭轸ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂┅ㄦ躅汜祆骖ㄦ躅汜祆殒脲ㄡ蝈箦篝狎舂ㄦ躅汜祆殒脲ㄡ蝈箦ǐ篝狎暴┅┅┅祜镳骘骝镯ǐ篝狎ㄩ轭轸博麸ō孱暴滹箦翩蝈篚祠ㄦ躅汜祆骖蝈篚祠ㄦ躅汜祆殒脲ㄡ蝈箦椹┅┅蝈篚祠戾è蝈篚祠ㄩ轭轸ㄦ躅汜祆骖ㄦ躅汜祆殒脲ㄡ蝈箦ō孱暴┅轭轸ㄦ躅汜祆骖ㄦ躅汜祆殒脲ㄡ蝈箦ō孱博┅ㄦ躅汜祆殒脲ㄡ蝈箦ō孱暴┅┅┅祜镳骘骝镯ō孱ㄩ轭轸畅滹黝麸篝狎滹箦翩蝈篚祠ㄦ躅汜祆骖ㄦ躅汜祆殒脲ㄡ蝈箦椹蝈篚祠┅蝈篚祠┅┅ㄤ彐躅蝈漉沐扉篝ㄦ箦骝镯孱篝狎孱脲轭轸轭轸皓ㄩ铛祆孱洎箦翩孱戾铉翳箦瘵┅ㄣ镱è篝狎癌蝈漉沐扉篝骖铘桡潋篝狎箦瘵骝镯孱ō孱篝狎舂脲轭轸轭轸皓è矧铛祆箦瘵ㄥ耢篝狎孱洎ㄩ轭轸轭轸ㄦ躅汜祆骖┅èō孱篝狎舂暴ㄩ轭轸ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅ㄦ蝻憝孱蝈漉沐鲥泗骖ㄣ镥蜚箦鲥泗矧篝狎孱脲轭轸轭轸皓è铛祆蝈篝箦瘵ㄩ轭轸ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅ㄦ躅汜祆殒脲ㄦ轵篝箦瘵┅戾è蝈篚祠ㄩ轭轸ㄦ躅汜祆骖轭轸ㄦ躅汜祆殒脲痫箦瘵┅ㄦ躅汜祆骖ㄦ躅汜祆殒脲痫箦瘵ㄦ躅汜祆殒脲痫箦瘵┅┅ㄩ孱祜镳蝈疱狒ō孱ㄩ轭轸博麒殪箦滹箦翩蝈篚祠ㄦ躅汜祆骖蝈篚祠ㄦ躅汜祆殒脲痫箦瘵┅┅祜镳麒殪箦滹箦翩蝈篚祠ㄦ躅汜祆骖蝈篚祠ㄦ躅汜祆殒脲痫箦瘵┅┅蝈篚祠┅┅ㄤ彐躅滹黠蜾螬麸瓠戾鲥飙痱秭啜ㄓ矿屙黠蜾ī┅┅ㄩㄢ秕钿с戾狎溻ㄣ戾狎溻┅换换惋溴涕箴御铘狲蔑眄镱涕箴换换蔑溴骝镯嗅蜥溟珥镦辽序镧蜥眄轭换换蔑瘗蜷玷ㄣ惫贡绣翦物蝣殓换换崎戾疳舡磲翥璁扉箴嗅趑弪磲翥桢骝镯箦泗轱懂换憎怩骈弩蛮议汨狎漆翦磲瞵蜿胬泱忮螂屐妁邃香麸忮共换澡忉箝狎轭狨骖螽扉箴祜镫骘⑿猎耘椅土悦壬吻屏蒙躺再ㄤ彐躅鲠蜷徕戾⑸鲠蜷徕戾ㄡ簌礅镬忮玳铑轭鏖翳嗫З竣ㄡ钿簌礅镬ㄥ聃犰ㄥ祠簌礅镬钺礤癌＼咯┅ㄤ彐躅疳舡磲翥疳趑弪轭瘐镳糸镱犰ㄢ轭溟铉铒忾钿轭珞┅⑼狒汨疳趑弪徵衢铙轭瘐轭翳泔铘屮镦翳忾钿轭珞ㄣ镱è羼忾钿轭珞驷殪驷殪è鲠蜷徕戾疳趑弪瞟磲翥璀鲠蜷徕戾疳趑弪轭瘐忾钿轭珞┅è羼疳趑弪轭瘐舂忾钿轭珞è箦珥孱舡疳趑弪瞽疳趑弪瞟箦珥孱舡磲翥桢疳趑弪轭瘐忾钿轭珞┅è箝铉戾疳趑弪瞽疳趑弪瞟箝铉戾磲翥桢疳趑弪轭瘐忾钿轭珞┅è犷ㄣ镱箴疳趑弪瞟ㄣ镱箴轭瘐舂疳舡磲翥蝈篝疳趑弪瞟蝈篝轭瘐舂疳舡磲翥ㄦ轵篝疳趑弪瞟ㄦ轵篝轭瘐舂忾钿轭珞┅驷殪┅箦翩ㄧ弭Э轶箝铉戾磲翥瑭ы狒汨轶箦翩ㄧ弭Э矧箝铉戾磲翥瑭ы狒汨矧箦翩ㄧ弭Э犷箝铉戾磲翥瑭ы狒汨犷洎箦翩ㄧ弭Э铒箝铉戾磲翥瑭ы狒汨铒舂箦翩ㄧ弭Э箦珥孱舡磲翥瑭箦珥孱舡磲翥瑭箦翩ㄧ弭Э箦珥孱舡磲翥瑭箦珥孱舡磲翥璜箦翩ㄧ弭Э箦珥孱舡磲翥瑭箦珥孱舡磲翥杩箦翩ㄧ弭Э殒箦珥孱舡磲翥瑭ы狒汨殒ㄤ彐躅箦珥孱舡疳趑弪瞽疳趑弪瞟⑸翳轶箦珥孱舡磲翥栝铉疳趑弪扉脲è开鲠颟疳舂竣ㄡ钿ㄣ镱箴疳趑弪瞟ㄣ镱箴ㄦ轵篝疳趑弪瞟簌礅镬ㄦ轵篝ㄦ轵篝疳趑弪瞟┅箦珥孱舡磲翥璀骖ㄦ轵篝ㄦ轵篝疳趑弪瞟┅┅ㄤ彐躅箝铉戾疳趑弪瞽疳趑弪瞟⑸翳轶箝铉戾磲翥栝铉疳趑弪羁女绠块痱邃殂狒濠酷钿疳趑弪铙匡疳趑弪铙┊ㄡ钿ㄣ镱箴疳趑弪瞟箝铉戾磲翥璀骖ㄦ轵篝疳趑弪瞟┅ㄤ彐躅箦珥孱舡磲翥桢疳趑弪轭瘐忾钿轭珞⒚犰翳蜷玷骢钽糸镱骘翳轶腴钿镦箦珥孱疳趑弪町ㄦ躅汜祆箦珥孱舡磲翥璀骖ㄦ轵篝ㄦ轵篝疳趑弪瞟┅疳趑弪轭瘐忾钿轭珞┅ㄤ彐躅箝铉戾磲翥桢疳趑弪轭瘐忾钿轭珞⒚犰翳蜷玷骢钽糸镱骘翳轶腴钿镦箝铉戾疳趑弪町ㄦ躅汜祆箝铉戾磲翥璀骖ㄦ轵篝疳趑弪瞟蝈篝疳趑弪瞟轭瘐忾钿轭珞┅ㄤ彐躅箦珥孱舡磲翥璀骖⑶弭翳箦珥孱舡磲翥骢钽糸镱骘殒轸轶簌礅镬翳狒栳镱瀹麒孱簌礅镬ㄧ弭箦珥孱舡磲翥瑭┅ㄤ彐躅箝铉戾磲翥璀骖⑶弭翳箝铉戾磲翥骢钽糸镱骘殒轸轶簌礅镬翳狒栳镱瀹麒孱簌礅镬ㄧ弭箝铉戾磲翥瑭┅ㄤ彐躅磲翥璀轶鲠颦犷洵痱邃轭瘐忾钿轭珞⒂蹉沐邃犷忾钿鲠殒翳轭瘐筢糸箧殄痱邃麒弪鲠颦犷洵痱邃轶翳扉篝鲠痱邃┊戾舄è鲠ㄦ轵篝鲠颦犷洵痱邃┅痱邃箦泔钿鲠颦犷洵痱邃┅铄鳝忾钿轭珞疳舡磲翥鲠轭瘐忾钿轭珞┅ㄩ矧ㄥ铄鳝忾钿轭珞驷殪铒ㄦ躅汜祆痱邃轭瘐舂┅驷殪铄鳝忾钿轭珞┅ㄤ彐躅磲翥璀犷疳趑弪铙轭瘐忾钿轭珞⒂蹉沐邃殒犰翳疳趑弪铙磲翥翳轭瘐舢ㄣ镱è羼忾钿轭珞驷殪驷殪è铛祆疳趑弪铙忾钿轭珞磲翥璀犷蝈篝疳趑弪铙轭瘐疳舡磲翥ㄦ轵篝疳趑弪铙轭瘐忾钿轭珞┅┅ㄤ彐躅磲翥璀矧疳趑弪铙轭瘐忾钿轭珞⒂蹉沐邃殒犷镱镦翳疳趑弪铙磲翥翳轭瘐舢ㄩ铛祆疳趑弪铙驷殪戾è铄鳝忾钿轭珞疳舡磲翥ㄦ轵篝疳趑弪铙轭瘐忾钿轭珞┅ㄩㄥ铄鳝忾钿轭珞驷殪磲翥璀矧蝈篝疳趑弪铙轭瘐忾钿轭珞铄鳝忾钿轭珞┅┅ㄤ彐躅磲翥璀铒疳趑弪铙轭瘐忾钿轭珞⒂蹉沐邃殒铒铄镦翳疳趑弪铙磲翥翳轭瘐舢澡轶鏖祆铄鲥忾钿犷鲠蜷徕戾螽ㄩ磲翥璀矧疳趑弪铙轭瘐忾钿轭珞驷殪忾钿轭珞┅ㄤ彐躅箦珥孱舡磲翥疳趑弪轭瘐忾钿轭珞镳糸镱犰篝狎癌⑼狒汨翳箦珥孱疳趑弪è开鲠颟疳舂徵衢铙轭瘐舢戾è鲠箦泔钿ㄦ轵篝疳趑弪瞟┅疳蝈篝疳趑弪瞟┅ㄩ铛祆疳舂磲翥璀鲠蜷徕戾鲠轭瘐忾钿轭珞戾è痫ㄦ轵篝磲翥璀痫ㄦ轵篝疳舂轭瘐篝狎舂┅ㄩ铛祆痫螬驷殪戾è獠疳舡磲翥疳篚怏羼轭瘐痫螬磲翥璀鲠蜷徕戾鲠篚怏羼轭瘐痫螬忾钿轭珞┅┅换涉翳轶磲翥驷殪邃趄犷雉桢祜铉弪镱ㄩㄥ獠驷殪箦珥孱舡磲翥疳趑弪轭瘐忾钿轭珞ǐ痫暴獠┅┅┅ㄤ彐躅骈蝮舡磲翥璀痫疳舯轭瘐篝狎舂⑵轭翳骈蝮痫箝糸镱翳狒疳舯泔蹯痫篌殁禊磲翥轭瘐衄篝狎糸铉狒痫箝糸镱篝狎舢涉疳舯轶铒瞽泔铙翎铘翳孱牾篝蝈趱蝾篝狎舢ㄣ镱è犷ㄡ麸疳舯铒鲠蜷徕戾疳舯┅痫箝糸镱疳舯轭瘐后翎螋篝狎呼弩＇羼踽飑è冀篝狎戾铉翳轭瘐舂篝狎舂华骈蜿卑悲共麽缉铋飑┅ㄤ彐躅箦珥孱舡磲翥璜疳趑弪轭瘐忾钿轭珞⑼狒汨镱矧盹蝈屐屙孱趔镦轭瘐舢箦珥孱舡磲翥疳趑弪轭瘐忾钿轭珞暴ㄤ彐躅箦珥孱舡磲翥杩疳趑弪轭瘐忾钿轭珞⑼狒汨弪矧镱屐屙孱镦轭瘐舢戾è鲠箦泔钿ㄦ轵篝疳趑弪瞟┅疳蝈篝疳趑弪瞟┅矧疳舡磲翥ㄣ镱鲠疳舂轭瘐忾钿轭珞疳舡磲翥疳轭瘐忾钿轭珞┅┅ㄤ彐躅磲翥璀殒疳趑弪轭瘐忾钿轭珞⒃弩犷狎忾趄狎屮痱弩箝镱轭鲲祧轭鲠蜷徕戾螽澡疳趑弪祜镫扉脲è块泔溴蝈篝┊换骈蜿卑悲共躞邃麸弼犰忾钿轭鲠祯弩ㄡ钿痱镧磲疸狎＇汜忾钿轭珞磲疸狎＇沅忾钿轭珞ㄥ鲠箦泔钿ㄦ轵篝疳趑弪瞟┅疳舡磲翥蝈篝疳趑弪瞟轭瘐忾钿轭珞┅ㄤ彐躅疳舡磲翥璀徕怛弼簌礅镬屮疳铙轱瞟⒛彐轭簌礅镬狍磲泸篝犷溟铉骘疳舡磲翥疳趑弪町箦翩ㄧ弭簌礅镬у疳钿疳舡磲翥璀徕怛弼ㄥ疳钿疳舡磲翥璀徕怛弼屮疳铙轱瞟┅ㄤ彐躅屮疳钿疳舡磲翥璀徕怛弼疳舂⑴疳钿秕犰疳趑弪磲翥栝铉徕怛弼獒糸镱轭疳舢ㄣ镱è犷簌礅镬疳舂ㄧ弭疳у疳钿疳舡磲翥璀徕怛弼┅è狒镯疳舂疳舂ㄣ镱ㄥ疳钿疳舡磲翥璀徕怛弼ㄦ轵篝疳舂ㄥ疳钿疳舡磲翥璀徕怛弼蝈篝疳舂┅┅ㄤ彐躅蝓戾忉箦洵趄犷箪狒矧ㄩ铕豸蝓戾脲磲翥桢ю狒磲翥瑭蝓戾殒＇骈蝮舂蝓戾翳孱＇蝈篝ㄡ泗轱＇篚忪轶┅⑵轭翳骈蝮蝓戾轭蝓戾翳狒磲翥桢轭瘐衄犷狃痨翳徙糸镱麸翳狒蝓戾箫礤＇灬礅溽蝓戾戾è蝈篚祠ㄦ躅汜祆磲翥桢ㄦ躅汜祆蝓戾殒蝓戾轭瘐舂┅ㄩ铒ㄥ蝈篚祠驷殪┅ㄦ躅汜祆徙糸镱蝈篚祠ㄦ躅汜祆蝓戾翳孱蝓戾┅┅蝓戾螬换惋溴涕箴御铘狲蔑眄镱涕箴换蔑溴骝镯嗅蜥溟珥镦硫糸骈汩犰深翦祆殓孱沐序镧蜥眄轭换蔑瘗蜷玷ㄣ惫贡绣翦物蝣殓换换崎戾躅殒扉箴疹殒殂狒轱骢钽糸镱换蝈聃轵弩疳繇狒汨ㄤ彐疳蜥礤翦镢沲蝮汨邈氇⒂栾蹯麇滹翳镢沲蝮汨邈肟ㄤ彐躅躅殒镳糸镱犰ㄢ轭溟铉铒忾钿轭珞┅⒂邋殒犷磲翥鏖翳玳鲥忾钿轭珞ㄣ镱è羼忾钿轭珞驷殪驷殪è羼忾钿轭珞è鲠蜷徕戾躅殒鲠蜷徕戾忾钿轭珞┅è鲠蜷徕戾躅殒鲠蜷徕戾忾钿轭珞┅è犷ㄣ镱箴ㄣ镱箴┅躅殒蝈篝蝈篝躅殒ㄦ轵篝ㄦ轵篝忾钿轭珞┅驷殪┅ㄤ彐躅躅殒鲠蜷徕戾鲠忾钿轭珞⒄铋纟鲠鏖翳躞轭ㄡ钿磲忮屮翦钿轭绌忾钿轭珞ㄣ镱è珏舡忾钿轭鲠忾钿轭珞躅殒祜镫躔鲠忾钿轭珞忾钿轭珞┅è犷鲠蜷徕戾ㄧ弭忾钿轭忾钿轭珞┅躅殒鲠祜镫躔忾钿轭珞忾钿轭珞┅è犷镢沲蝮汨邈氇镢沲蝮汨邈鲠忾钿轭珞┅驷殪ㄥ翦钿忾钿轭珞鲠忾钿轭珞┅┅ㄤ彐躅镢沲蝮汨邈鲠忾钿轭珞⒛镥鲠镢沲犷麒弪轭箝溴竣ㄣ镱è羼鲠舂è犷鲠蜷徕戾ㄧ弭忾钿轭忾钿轭珞┅镢沲蝮汨邈鲠祜镫躔忾钿轭珞忾钿轭珞┅è泔铙矧镢沲蝮汨邈鲠ㄦ轵篝忾钿轭珞镢沲蝮汨邈鲠蝈篝忾钿轭珞┅铋飑┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅篚怏舡忾钿轭珞ㄢ轭溟铉⒂踱篝轸豸翳鲠祯镦鲠蜷徕戾轭忾钿轭珞轭麸翎腴铉蝈沲蝮轹屐怙躅鲠蜷徕戾轭麸徙泔躅舢ㄣ镱è羼忾钿轭珞驷殪驷殪è羼忾钿轭珞铒忾钿轭珞è犷鲠蜷徕戾ㄧ弭忾钿轭忾钿轭珞┅篚怏舡忾钿轭珞忾钿轭珞祜镫躔忾钿轭珞┅è狒镯蝈躞瀛泔铙篚怏舡忾钿轭珞忾钿轭珞ㄣ狎┅篚怏舡忾钿轭珞忾钿轭珞ㄣ潋┅┅┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐躅躅殒殄⒁弭躜箫礤翳轭翳狒躅殒殄鏖翳怙翳犷矧驷殪┊篚怏舡忾钿轭珞躅殒┅换换惋溴涕箴御铘狲蔑眄镱涕箴换换蔑溴骝镯嗅蜥溟珥镦辽序镧蜥眄轭换换蔑瘗蜷玷ㄣ惫贡绣翦物蝣殓换换崎戾痱镬镧扉箴痱镬镧骝镯ū碑畅鏖翳轭翦蜥泗轹忉汶趄徙腴铉换蝈聃轵弩Ⅴ铋纟滹弩铒蝈聃轵痱镬镧雹换换滹弩铒轭沆蹁溴篝蝓泗轹躅殒殂狒轱ū碑订箦痱镬镧惝扉箴换沆狨箦狎蝈痱弩孱翦狍ㄨ遽怙澌泔铙沐祆ㄤ彐躅沆狨箦桢徜ㄣ灬躞濠ㄦ轵篝沆狨箦┅ㄤ彐躅沆狨箦怙澌ㄣ灬躞濠蝈篝沆狨箦┅换沆狨箦狎篝矧邃镱翳痱邃殂狒濮痨轶ㄤ彐躅珏舡沆狨箦痱邃ㄧ弭痱邃с灬躞弩┅ㄤ彐躅痱邃殂狒蝈灬糸镱ㄦ轵篝蝈灬糸镱┅ㄤ彐躅狎珞⒃桢狎珲礤铘镦蝈灬糸镱蝈篝┅ㄤ彐鲠溻痱邃殂狒弩铋⑨扉篝镦犰痱邃殂狒弩篝矧邃轭翳溽翎忉箦ㄤ彐磲泸辑é蝈篝沆狨箦⑨滗沆狨箦麸翳溽翎忉箦啜徜洵沆狨箦К蝈痨徙瀛凯鲠蝮沆狨箦┅ㄤ彐躅徜洵沆狨箦ㄣ灬躞濠⑨滗沆狨箦麸翳溽翎忉箦轭溴邃怡桢徜痱邃殂狒瀹换翳痱邃殂狒眭篝忮铒瞽鲠蜷徕戾簌礅镬戾è痱邃痱邃殂狒ㄣ灬躞瀛桢徜沆狨箦┅┅ㄡ篌弪ㄡ钿簌礅镬痱邃铒鲠蜷徕戾痱邃┅┅瘐箬铄痱邃溻痱邃殂狒弩箦翩ㄧ弭痱邃с灬躞弩钽镱ㄧ弭沆狨箦痱邃扉篝沆狨箦┅痱邃┅ㄤ彐躅沆遽颦溻īⅡ屙秭犰沆狨箦ㄦ矧犰痱邃殂狒弩骝镯翳溽翎忉箦磲疸＇沆遽颦痱邃殂狒溻痱邃殂狒弩┅ㄤ彐躅沆遽颦痱邃殂狒痱邃殂狒濠Ⅱ屙秭翳沆狨箦骘箝铉戾痱邃殂狒瀹箦翩ㄧ弭痱邃殂狒с灬躞弩铋飑ㄤ彐躅蝈钺礤鲠蜷徕戾Ⅱ屦灬沐犰鲠蜷徕戾轭鏖翳铄镱弩篚忪轶磲疸狎＇灬礅溽鲠颟ㄣ镱鲠ㄧ孱簌篝蜷铉鲠颟┅鲠蜷徕戾蟓轭┅┅ㄤ彐躅躅轳蹂骈钿犷麒弪瀛殒痱邃殂狒趄邋镳糸镱犰骘躅洵箫驷颟Ⅱ弭躜扉篝镦戾狯弩镦趄邋筢糸箧轭痱邃殂狒瀣鏖翳漉痨殂狒弩蝈盹鲥洚ㄩㄡ麸趄邋ㄩㄦ躅汜祆痱邃殂狒趄邋ㄡ潢镩趄邋骘躅洵箫驷颟骘躅洵箫驷颟躅轳蹂骈钿犷麒弪瀛殒痱邃殂狒ㄦ轵篝趄邋躅轳蹂骈钿犷麒弪瀛殒痱邃殂狒蝈篝趄邋骘躅洵箫驷颟┅ㄤ彐躅骈钿犷麒弪瀛殒痱邃殂狒趄邋滹弩痱邃殂狒狃痨麸犷狒镯轭翳趄邋竣ㄩㄡ麸趄邋ㄦ躅汜祆痱邃殂狒趄邋矧ㄦ轭洵犷麒弪瀛殒痱邃殂狒ㄦ轵篝趄邋┅ㄦ轭洵犷麒弪瀛殒痱邃殂狒蝈篝趄邋┅┅ㄤ彐磲泸凯é蝈篝顼犰螬啜麸瓠戾鲥飙痱秭К蝈痨徙瀛凯鲠蝮顼犰螬┅ㄤ彐躅痱秭瀛犰ㄧ镝祗忾钿轭珞⑵轭箫祯糸镱麸翳泔铌躅泗轱镦顼犰螽ㄣ镱è羼忾钿轭珞驷殪驷殪è铛祆顼犰螬忾钿轭珞痱秭ㄦ轵篝顼犰螬忾钿轭珞蝈篝顼犰螬┅┅ㄤ彐躅痱秭ㄧ镝忾钿轭珞雉桢颦顼犰螬⒁弭躜扉篝镦痫篌殁戾箫祯糸镱麸顼犰戾è沆狨箦ㄧ弭沆狨箦痱邃殂狒顼犰┅┅ㄩ扉篝沆狨箦螬箫礤＇灬礅溽ㄣ灬躞濠戾è铄鳝沆狨箦蝈钺礤鲠蜷徕戾沆狨箦┅痱秭瀛犰ㄡ痧孱ㄣ灬躞瀛怙澌铄鳝沆狨箦雉桢颦顼犰螬躅殒顼犰ㄣ灬躞瀛桢徜铄鳝沆狨箦忾钿轭珞┅┅沆狨箦螬换澡痱邃殂狒濮沆狨箦螈汜忮犷狒镯换痱轫轸轹骢钽糸镱麸汜祆ㄦ躅汜祆沆狨箦蝈篝顼犰忾钿轭珞雉桢颦顼犰螬┅ㄤ彐躅麸瓠戾鲥飙痱秭ㄧ镝祗痱秭瀛犰啜犁镝祗箬秣痱镬镧鲠蝮括鲠蜷徕戾蟓轭顼犰螬┅铒忾钿轭珞ㄦ矧磲ξ锂鲠祯弩┅ㄤ彐躅箬秣痱镬镧鲠蝮鲠蝮忾钿轭珞雉桢颦顼犰螬⑿蜷铘遽汨鲠蜷徕戾鏖翳轸忾钿轭绠澡孱狍翳躞弪殒盹蝈箫祯糸镱狎溴箝蝈洚ㄩ铛祆鲠蝮ㄦ矧磲馘螈ㄤ镬轶鲠鲠蝮ㄦ矧磲幄鲠篚怏舡忾钿轭珞忾钿轭珞鲠颟┅ㄩㄣ镱糸铛瀛皓驷殪痱秭瀛犰雉桢颦顼犰忾钿轭珞┅箦翩ㄧ弭箬秣痱镬镧鲠蝮с灬躞弩箬秣痱镬镧鲠蝮ㄤ彐躅泔铘轭蹂ī⒘箅躞弪殒麇箬秕熹泔铘轭蹂祜镫轭骘箫祯糸镱螽ㄣ狍蝈徜汨狎ǎ芑舂ǎ墚铋飑ǎ茴鬻扉铄ㄣ镱糸铛瀛皓雉桢蝼轶ㄦ矧磲赠疱麸箦盹蝈矧麸篝镳ㄣ镱糸铛瀛皓┅ㄤ彐躅鲠蜷徕戾蟓轭ㄥ皓⒁弭躜扉篝镦犰翳鲠蜷徕戾轭咆挟躅轳蹂骈钿犷麒弪瀛殒＇铒瞽犷镱鲠蜷徕戾屮皓ㄤ彐躅铒瞽犷镱鲠蜷徕戾ㄡ钿鲠蜷徕戾铒ㄥЭ┅┅ㄤ彐躅蝈痨徙瀛凯鲠蝮ㄥ皓⒁屦灬沐犷鏖翳轭屮鏖翳鲠镦翳骘蝽勘渤ㄣ镱è羼屮Эㄧ孱簌⒖┅è狒镯屮皓屮皓蝈躞瀛泔铙蝈痨徙瀛凯鲠蝮ㄦ轵篝屮皓蝈痨徙瀛凯鲠蝮蝈篝屮皓屮皓┅换换惋溴涕箴御铘狲蔑眄镱涕箴换换蔑溴骝镯嗅蜥溟珥镦辽序镧蜥眄轭换换蔑瘗蜷玷ㄣ惫贡绣翦物蝣殓换换崎戾痱镬镧惝扉箴崎钺鲥蝮轱镦翳泔眇殪弪换换轭沆蹁轭犰轫痱秭屙孱趔骝镯翳汨狃翦虍换蝈聃轵弩痱镬镧ㄤ彐泔铙翎铘躅怙躅⒄钼秕钿ㄤ彐篝蝓泗鲠钺礤ㄢ轭溟铉躅怙躅洎ㄤ彐躅怙躅洵鲠颟铒ㄥ鲠颦忾钿轭鲠颟躅怙躅洎┅ㄤ彐磲泸溴蝈ㄥ皓⑵镬祜痫轭翦蝮骘怙躅鲠蜷徕戾螽啜痱镧祜镳麒殪ㄡ钿鲠颦屮皓ㄢ秕钿屮皓滹箦翩屮鲠颦忾钿轭屮皓┅屮皓ㄤ彐躅躅殒⒛弩趄蹉糸鲥禊躅殒赭屮痱弩箝镱螈ㄣ镱è羼ㄤ弪彐ㄤ弪彐┅舂è鲠颦箦舡忾钿轭纭┅è鲠颦箦舡忾钿轭纭┅è犷ㄣ镱箴ㄣ镱箴┅ㄡ钿躅殒ㄦ轵篝ㄦ轵篝┅躅殒蝈篝蝈篝┅┅铋飑┅ㄤ彐躅箦舡忾钿轭纭鲠鲠祯濠⒂弭鲠颛忾钿轭麸鲠祯瀹领麽篚沣邋潴蝈趱蝾舂箦翩鲠颦忾钿轭鲠颟鲠祯濠舂ㄤ彐躅痱轭舡鲠鲠篝蝈犴溴痿瑭ㄩ矧ㄡ钿痱轭舡戾鲥飒窘溴痿痱轭舡戾鲥飒┅鲠颦ㄤ弪彐鲠颟┅ㄦ矧磲篝蝈犴⒖幄鲠颦钺礤鲠颟黩轸鲠后趄遽篝蝈犴┅ㄤ彐鲠趄衢飒磲脲狎蜥舶烘殪飙痫轭翦横潢躞翎忪舂ㄤ彐躅箦舡忾钿轭纭鲠鲠祯濠⒂弭鲠颛忾钿轭麸鲠祯瀣徭翦筢鲩铉翳鲠蜷徕戾轭翳趄衢飚领麽蝈趱蝾舢躅戾篌ㄥ鲠鲠祯濠鲥泗矧瘐箬屮翦钿鲠趄衢飒箦翩鲠颦忾钿轭鲠颟鲠祯濠舂ㄤ彐躅躅滹忾钿轭珞镬洵趄衢飑⒄钿犰忾钿轭珞忉汶麸玳鲥痫轭轭翳趄衢飚祜镳躅糸ㄦ殪飙痫轭翦趄衢飒镬洵趄衢飑滹箦翩鲠颦忾钿轭鲥泗矧痫趄衢飒┅躅怙躅洎┅ㄤ彐鲠鲠颦泔躅翦颡癌ㄤ彐篝蝓泗鲠ê泔铙趄蹉麸īê痱轭舡骢钽糸镱痱轭舡鲠颟钺礤ㄩ钽鲠颦泔躅翦颡┅ㄢ轭溟铉躅怙躅洎ㄤ彐躅痱镬镧泔眇殪簌礅镬镳糸镱犰ㄣ灬躞弩ㄧ弭沆狨箦簌礅镬┅⒚镯痖戾簌礅镬磲脲箦疳蜥翦骢钽糸镱骘遽汨狎轸躅戾篌铛祆沆狨箦螬戾è狎轸蝈灬糸镱狎轸ㄣ灬躞瀛桢徜ㄦ轵篝沆狨箦螬┅┅换蔑眇殪翳沆狨箦鏖翳翳轶狎轸ㄣ镯痖戾痱邃殂狒簌礅镬狎轸ㄣ灬躞弩鏖翳狎轸沆狨箦＇狎轸┅换蔑眇殪犰翳沆狨箦鏖翳犷雉桢狎轸痱镬镧泔眇殪簌礅镬ㄣ灬躞弩鏖翳狎轸沆狨箦＇狎轸┅┅ㄤ彐躅沆狨箦蟓鏖翳狎轸ㄣ灬躞弩翦篝狎轸⒁弭躜犰沆狨箦麒矬桢徜栳玳鲥狎轸ㄦ轭洵犰狎轸沆狨箦弘妁＇灬礅溽ㄣ灬躞濠蝈灬糸镱狎轸ㄣ灬躞瀛桢徜沆狨箦┅呼弩翦篝┅ㄤ彐躅蝈灬糸镱狎轸蝈灬糸镱⒃桢铛礅弪镦狎珲礤铘麸蝈灬糸镱砒犴痨搴蝈灬糸镱狎轸Ж悌骄尝戾铉翳ㄡ蜱蝈灬糸镱┅ㄤ彐躅狎珞⒃桢狎珲礤铘镦蝈灬糸镱蝈篝┅ㄤ彐躅磲脲疳蜥礤翦蝮ㄡ蜷豉⒁弭躜翳扉篝酷蜱酷蜱酷蜱狎轸祜镳骘骝镯麸狎轸泔祆邈铄鳝簌礅镬Э狎椹┅ㄤ彐躅磲脲痱邃殂狒簌礅镬狎轸⒁弭躜翳簌礅镬簌礅镬狎轸簌礅镬簌礅镬Н狎轸┅ㄤ彐躅磲脲啜┅ㄤ彐躅泔眇殪瀛汜祆痱邃殂狒狎珞泔铘⒚镯痖戾汜祆麸痱镬镧痱邃殂狒瀹啜痱邃殂狒泪蜱泔铘┅ㄤ彐躅痱镬镧泔眇殪弪磲泸钺礤⑵弭汨翳泔眇殪弪磲泸骘序镬镧痱邃殂狒瀹换物翦瘟团轶翳蜥钺礤铒翳钺礤狎轸ㄧ弭钺礤ю蝻祜绛泔眇殪弪磲泸铹ㄤ彐磲泸溴姝痱镬镧泔眇殪弪磲泸钺礤狎珈轶怙澌怙澌⒛彐轭泔眇殪弪磲泸骘序镬镧啜箦翩ㄧ弭К钺礤ю蝻祜绛泔眇殪弪磲泸铹＇灬礅溽狎珈轶怙澌┅ㄤ彐躅泔眇殪瀛狎ㄡ蜱⑶孱弪狒泔溴骘犷狎珲礤铘麸顼犰轭翳怙澌ㄣ镱è鲠蜷徕戾狎绌狎绌è铒ㄨ狍鲠蜷徕戾狎绌唰狎绌è痱镳弪扉篝狎绌啜扉篝磲疸狎＇泔眇殪瀛狎狎绌┅啜泔铙ㄣ镯痖戾狎ㄦ轵篝狎绌ㄣ镯痖戾狎蝈篝狎绌┅┅ㄤ彐躅栳蟓鲠蜷徕戾⑸翳弪鲠蜷徕戾犷麒弪轭翳屮痱弩箝镱竣ㄦ轭洵殒犷麒弪＇鲠蜷徕戾┅ㄤ彐躅痱镳弪扉篝⑸痱镳弪铒瞽滹趑邃扉篝竣矧铛祆ㄡ钿ㄣ镱箴痱镳弪扉篝蝈篝┅┅ㄤ彐躅磲忮徜洵躅滹忾钿轭珞ㄣ镯痖戾洵屮痼⒄钿犷忾钿轭珞翳狒铄邃躅滹轭绠涉翳弪狎犷忾钿翳趄衢忮骘蝈麇篝狎舢ㄩ戾铉翳奖泔眇殪邃屮痼泔眇殪邃屮痼啜戾è镬洵趄衢ㄦ殪飙痫轭翦趄衢飒┅ㄦ轵篝泔眇殪邃屮痼括祜镳骘屮轭蝈篝泔眇殪邃屮痼泔祆邈Ж躅滹忾钿轭珞镬洵趄衢飑泔祆邈屮皓┅┅ㄤ彐躅忾钿躅怙躅洵鲠蝮疳蜥礤翦蝮屮皓⑸翳弪狎犷鲠蜷徕戾轭屮ㄢ弩殇弩翳疳蜥礤翦蝮翳孱忾钿翳屙麸铄鲠蝮戾è屮瓠鲠蝮箦舡溟骀弪孱沐鲠蜷徕戾蟓轭屮皓疳蜥礤翦蝮┅ㄩ屮瓠鲠蝮啜戾磲疸狎＇灬礅溽鲠颟啜鲠咯┅屮瓠鲠蝮屮皓屮皓┅ㄤ彐磲泸辑é蝈篝沆狨箦⒘滗沆狨箦麸翳溽翎忉箦啜徜洵沆狨箦К磲脲犷镱盹躞沆狨箦┅ㄤ彐躅磲脲犷镱盹躞ㄥ镳糸镱犰ㄡ铒瞽鲠蝮ㄡ铒铢盹躞鲠蜷徕戾蟓轭屮皓┅⒁屦灬沐鲠蜷徕戾翳狒狎镱禊躞邃镱沐鏖翳慨ㄣ镱è泔铙屮皓蝈躞瀛泔铙磲脲犷镱盹躞ㄦ轵篝屮皓犷镱鲠蝮磲脲犷镱盹躞蝈篝屮皓犷镱鲠蝮屮皓è礤礅弪屮犷镱鲠蝮Э屮皓┅ㄤ彐躅犷镱盹躞鲠蜷徕戾蟓轭趄邋⒁弭躜扉篝镦犰鲠蜷徕戾翳狒镢沲镱禊镱沐轭趄邋鲠祯弩ㄡ铒瞽鲠蝮轭趄邋铋铋飑┅ㄤ彐躅犷镱鲠蝮轭趄邋箦孱镱沐箦孱盹蝈⒆犰翳溽翎篝蝓泗躜砸排蝈趱蝾轭扉篝镦鲠蜷徕戾篌箦孱镱沐犷扉篝镦鲠蜷徕戾箦孱盹蝈翳犷镱沐ㄣ镱è泔铙趄邋眭祠轲戾鲠祯瀛忾钿铄鳝箦孱镱沐铄鳝箦孱盹蝈ㄡ铒瞽鲠蝮轭ㄦ轵篝趄邋箦孱镱沐箦孱盹蝈ㄡ铒瞽鲠蝮轭蝈篝趄邋铄鳝箦孱镱沐铄鳝箦孱盹蝈┅è铒鲠蜷徕戾趄邋┅鲠祯弩箦孱镱沐箦孱盹蝈┅è礤礅弪趄邋箦孱镱沐鲠祯弩ㄤ屐弭趄邋箦孱镱沐ㄣ镱趄邋箦孱盹蝈┅è礤礅弪趄邋箦孱盹蝈鲠祯弩箦孱镱沐箦孱盹蝈┅鲠祯弩ㄣ镱趄邋箦孱镱沐箦孱盹蝈┅┅ㄤ彐躅泔眇殪瀛躅殒忾钿轭珞⒁弭躜鲠祯弩泔溴麸翦篝殒犷躅殒犷铄忾钿轭扉篝ㄣ镱换疹殒泔铙翎铘犷泔铙弩冕箦è铒矧ㄨ狍鲠蜷徕戾ㄨ狍鲠蜷徕戾┅爆鲠祯弩ㄥ聃犰忾钿轭珞┅è犷ㄣ镱箴ㄣ镱箴┅眭祠轲戾鲠祯瀛忾钿ㄣ镤灞忾钿轭珞暴ㄣ镯痖戾躅殒ㄦ轵篝ㄦ轵篝忾钿轭珞眭祠轲戾鲠祯瀛忾钿ㄣ镤宀忾钿轭珞博ㄣ镯痖戾躅殒蝈篝蝈篝忾钿轭珞暴鲠祯弩ㄣ镯痖戾殒泔溴泔溴博忾钿轭珞博┅换儒蝈矧轶鲠蜷徕戾虚汶翳蜷玷镱搴è鲠蜷徕戾ㄣ镯痖戾躅殒鲠蜷徕戾忾钿轭珞┅ㄣ镯痖戾躅殒鲠蜷徕戾忾钿轭珞┅┅ㄤ彐躅泔眇殪瀛殒痱邃翳孱疳螋⒚镯痖戾涕箴善骘蝽物屐箦疳螋犰祜麇洚ㄣ狍痱邃è舂翳孱疳螋è铋飑铋飑雉桢蝼轶啜殒痱邃翳孱疳螋┅┅ㄤ彐躅泔眇殪瀛躅殒鲠蜷徕戾忾钿轭珞⒇轶鲠蜷徕戾犷磲忮戾舄èㄦ镬祜鳝忾钿轭忾钿轭珞┅ㄩㄣ潋猢┅ㄩ鲠蜷徕戾ㄦ镬祜鳝忾钿轭忾钿轭珞┅ㄩㄣ潋猢┅ㄣ镱冕箦è矧ㄥЭㄥЭ┅鲠祯弩忾钿轭珞┅辈è铒ㄡ钿ㄥ聃犰暴ㄥ聃犰暴┅溴蝈ㄣ镯痖戾躅殒忾钿轭珞┅è骈钿犷麒弪暴鲠祯弩铋忾钿轭珞┅北è泔铙暴番卑鲠祯弩啜躅殒ㄣ镯痖戾狎忾钿轭珞┅ㄢ轭洵鲠蜷徕戾蟓轭忾钿轭珞┅è铒铛祆猢换楫瀹轶犷酷蜱鲠蜷徕戾ㄩㄡ钿鲠蜷徕戾暴铛祆猢鲠祯弩ㄥ翦钿忾钿轭珞忾钿轭珞┅鲠祯弩啜躅殒ㄣ镯痖戾狎忾钿轭珞┅ㄥ翦钿忾钿轭珞忾钿轭珞┅┅惮è铒铛祆猢ㄣ镯痖戾躅殒鲠蜷徕戾忾钿轭珞┅鲠祯弩ㄥ翦钿忾钿轭珞忾钿轭珞┅┅┅脯ㄤ彐躅忾钿鲠蜷徕戾蟓轭ㄥ忾钿轭珞⒙轭犰鲠蜷徕戾轭屮麸翳屙箦祧弩犷徜翳狒麸忾钿轭珞ㄥ沐痿骘鲠蜷徕戾犰蝈徜怙躅洎ㄤ镬轶鲠鲠蜷徕戾蟓轭屮皓躅戾篌ㄧ弭忾钿轭鲠忾钿轭珞箦翩忾钿轭珞ㄥ翦钿忾钿轭珞鲠鲠忾钿轭珞┅┅忾钿轭珞ㄤ彐躅骘祆秣忾钿轭鲠忾钿轭珞⑶弭翳蹯糸磲翦忾钿轭镦鲠徙泔蜾轭麸忾钿轭珞戾èㄧ弭忾钿轭鲠忾钿轭珞┅ㄩㄥㄣ狎猢ㄣ潋猢矧ㄦ镬祜鳝忾钿轭ㄣ潋猢忾钿轭珞猢┅ㄤ彐躅泔眇殪瀛狎ㄡ蜱忾钿轭珞⑶孱弪狒泔溴骘犷狎珲礤铘麸顼犰轭翳怙澌ㄣ镱è羼狎ЭЖ咯è鲠蜷徕戾狎绌戾è忾钿轭ㄧ弭忾钿轭狎忾钿轭珞┅ㄩㄡ钿铒铛祆忾钿轭绌铒ㄥ狎ㄢ轭溟铉鲠忾钿轭绌┅ㄣ镯痖戾狎ㄢ轭溟铉鲠忾钿轭绌忾钿轭珞狎绌┅è铒ㄦ轭洵殒犷麒弪＇鲠蜷徕戾狎绌唰狎绌è痱镳弪扉篝狎绌啜扉篝磲疸狎＇灬礅溽ㄡㄣ镯痖戾狎忾钿轭珞┅狎绌┅啜泔铙ㄣ镯痖戾狎ㄦ轵篝狎绌忾钿轭珞ㄣ镯痖戾狎蝈篝狎绌忾钿轭珞┅┅ㄤ彐躅忾钿铄鳝鲠蜷徕戾ㄢ轭溟铉顼犰⑴翦钿忾钿轭珞麸轭沆蹁犷躅怙躅鲠蜷徕戾轭顼犰戾è鲠蜷徕戾蝈盹鲥殒＇灬礅溽雯ㄡ篌镢忾钿轭珞┅鲠蜷徕戾蟓轭顼犰┅┅钽镱磲疸狎＇箦戽泔铙鲠蜷徕戾螬忾钿轭珞┅ㄤ彐躅箦戽泔铙ㄣ镱┅ㄤ彐痱镬镧泔眇殪弪磲泸ㄧ镝怙澌泔铘忾钿轭珞⒚镯痖戾顼犰麒殂轶汜祆麸疆戾è狎珞ㄡ蜱顼犰┅ㄩǒ戾铉翳狎珞博吼狍换溴沆轭麸栳钿戾翳轶顼犰眭祠轲戾鲠祯瀛忾钿ㄣ镤灞忾钿轭珞暴ㄣ镯痖戾躅殒ㄦ轵篝狎珞箦泔钿狎珞忾钿轭珞ㄣ镯痖戾殒泔溴ㄣ镯痖戾怙澌怙澌泔铘忾钿轭珞暴┅┅ㄤ彐躅泔眇殪瀛沆狨箦疳蝽沆狨箦泔铘⒃蜥铙骘蝽狩狴翳桢徜犷泔眇殪翳蝈篚祠轭怙澌ㄢ轭洵躅怙躅洵鲠蝮疳蝽ㄣ镯痖戾怙澌钽镱磲疸狎＇磲脲疳蝽ㄡ蜱ㄣ灬躞瀛桢徜沆狨箦┅ㄣ灬躞瀛怙澌沆狨箦┅泔铘磲疸狎＇箦戽泔铙疳蝽螬┅华ㄤ彐鲠躅泔眇殪邃铋⑿蝻祜簌礅镬翳狒栳鲥铒忮孱泔眇殪邃ㄤ彐躅徜洵沆狨箦ㄣ灬躞濠⒘滗沆狨箦麸翳溽翎忉箦轭溴邃怡桢徜痱邃殂狒瀹换澡痱邃殂狒眭篝忮铒瞽鲠蜷徕戾簌礅镬戾è痱邃痱邃殂狒ㄣ灬躞瀛桢徜沆狨箦┅┅ㄡ篌弪ㄡ钿簌礅镬痱邃铒鲠蜷徕戾痱邃┅┅瘐箬铄痱邃溻痱邃殂狒弩瘐箬铄痱邃躅泔眇殪邃华箦翩ㄧ弭痱邃с灬躞弩钽镱ㄧ弭沆狨箦痱邃扉篝沆狨箦┅痱邃┅ㄤ彐躅麸瓠戾鲥飙痱秭ㄧ镝祗⑿蝻鲥翳扉篝镦顼犰怡泔眇殪轭犷汜祆轭轸换崎蝮蝈溴骈铄麸瓠戾鲥飙聃弪ㄣ戾狎痱邃殂狒麸瓠戾鲥飙聃弪戾è鲠蝮ㄤ屐弭Э鲠蜷徕戾蟓轭顼犰螬┅ㄡ滗沆狨箦啜麸瓠戾鲥飙聃弪犁镝祗箬秣痱镬镧鲠蝮磲疸狎＇簌礅镬钺礤鲠蝮鲠蝮┅┅换物蝓轸蝓瞽痱镬镧麸瓠戾鲥飙聃弪＇殓铒蝈ㄦ矧磲ξ锂鲠祯弩┅ㄤ彐躅蝓瞽痱镬镧痱镢邃躜泔铘⒁躅碍狎痱镬镧痱镢邃躜鏖翳玳鲥泔铘轭踽糸镱换崎蝮泔眇殪犷翳轭屐箦翳狒铄邃轸痱镬镧泔眇殪瀛簌礅镬螬换义箦翳趄衢犷翳铄鲠蜷徕戾泔躅翦箦翩ㄦ殪飙痫轭翦趄衢飒癌箦翩鲠颦泔躅翦颡癌换崎钺祆汜祆翳聃弪ㄣ狒汨麸瓠戾鲥飙痱秭ㄦ躅汜祆痱镢邃躜泔铘┅ㄤ彐躅痱镬镧泔眇殪瀛簌礅镬é镳糸镱犰簌礅镬躅泔眇殪邃┅⒚镯痖戾扉篝镦序镬镧簌礅镬螽蛮溴驷蹯衄翳扉篝轶犰簌礅镬翳狒铄邃轸磲疸＇痱镬镧泔眇殪簌礅镬螬箦翩躅泔眇殪邃箦舡溟骀弪孱沐躅泔眇殪邃簌礅镬螬┅ㄤ彐躅殓铒蝈é蝈篝狎珞ㄤ邈灬蝈ㄩ珙矧狎珞┅铋飑ㄤ彐躅箬秣痱镬镧鲠蝮鲠颦钺礤鲠蝮泔铘⒛轶痨狴翳鲠蜷徕戾蟋犷痱镯痿翳躞弪麸箦殒麇箬秕熹泔铘轭蹂涉铒衄蝈趱蝾麸翳麸戾鲥飚ㄩ铛祆鲠蝮ㄦ矧磲馘螈祜镳骘钺礤轭鲠颦钺礤骘鲠轭鲠蝮滹ㄦ矧磲幄钺礤ㄤ弪彐屮鲠颟┅ㄩㄣ镱糸铛瀛皓ㄦ躅汜祆泔铘翳蝻麸瓠戾鲥飙痱秭铋飑┅ㄤ彐躅溴蝈姝屮ㄥ皓⒙蹰熹箫礤翳轭羼蹰鲠戾铘麸咆鏖翳鲠蜷徕戾溴蝈驽蝈钽邃ㄩㄡ麸ㄤ弪彐屮皓屮蝈躞瀛泔铙ㄤ弪彐屮ㄦ轵篝屮皓ㄤ弪彐屮蝈篝屮皓屮皓┅ㄤ彐鲠痱邃殂狒濯铋⒃桢序镬镧痱邃殂狒沲蝌孱綮忮轭泔眇殪邃ㄤ彐躅泔眇殪瀛痱邃殂狒簌礅镬狎轸沆狨箦螬⒚镯痖戾犰翳沆狨箦骘玳鲥簌礅镬狎轸轭麸箝铉戾躺有骢钽糸镱戾è痱邃殂狒濯磲脲痱邃殂狒簌礅镬狎轸┅华疳蜥礤翦蝮磲脲疳蜥礤翦蝮狎轸┅ㄣ镯痖戾ㄥ鲠啜溴骢痱邃殂狒濯ì鲤狎犴弭弪泔铘磲忮徜洵躅滹忾钿轭珞磲疸狎＇灬礅溽ㄣ灬躞濠ㄣ镯痖戾沆狨箦疳蜥礤翦蝮沆狨箦с镱舂沆狨箦螬┅┅┅ㄤ彐躅泔眇殪瀛怙澌ㄢ镤泔铘忾钿轭珞⒚镯痖戾翳怙澌镦沆狨箦ㄣ镱è铛祆怙澌啜骢钽犰泔铘┅è羼ㄦ轵篝怙澌А华啜痱镧ㄣ镯痖戾怙澌蝈篝怙澌泔铘忾钿轭珞华蝈趱蝾骝镯痱邃殂狒濯铋飑┅华戾舄è顼犰ㄦ轵篝怙澌┅磲泸痱镬镧泔眇殪弪磲泸痱邃殂狒顼犰┅磲泸锃鲠ㄩ磲泸ㄦ躅汜祆磲泸顼犰蝈篝怙澌泔铘忾钿轭珞┅┅ㄩㄡ钿磲泸铒ㄥ磲泸锃鲠吼狍螬┅磲泸锃鲠啜磲脲痱邃殂狒痱邃殂狒顼犰蝈灬糸镱狎轸顼犰┅括磲疸狎＇灬礅溽ㄡ蜱ㄣ镯痖戾狎狎忾钿轭珞┅ㄡ蜱顼犰┅ㄩ铛祆蝈篝怙澌┅泔铘啵Ж灬礅溽īㄣ镯痖戾怙澌蝈篝怙澌泔铘ㄢ轭洵铄鳝鲠蜷徕戾忾钿轭珞顼犰┅┅┅┅┅换换惋溴涕箴御铘狲蔑眄镱涕箴换换蔑溴骝镯嗅蜥溟珥镦辽序镧蜥眄轭换换蔑瘗蜷玷ㄣ惫贡绣翦物蝣殓换换崎戾痱镬镧沭扉箴序轫轸轹弩骘翳痱镬镧泔眇殪弪换换铄邃邃麸徙趱犰禊蝓箫礤骢钽糸镱螽换迈骈怡龄犴漆蝰蹊狎驷蝰蹊狎楞螽豸屮狍邃醍换则轹獒漆蝰蹊狎轶物蝣殓泔躞轭换蝈聃轵弩痱镬镧恽ㄤ彐躅蝈徜ㄥ泔铘ㄩ躅殒屮蝈徜┅ㄦ躅汜祆泔铘┅ㄤ彐躅黩轸瀵ㄥ泔铘黩轸ㄤ弪彐屮屮皓吼蝈趑舂ㄦ躅汜祆泔铘┅ㄤ彐躅铎ㄣ镱舂翦蝠蜷ㄦ躅汜祆泔铘┅ㄤ彐躅蒋酷蜱酷蜱泔铘ㄩ躅殒酷蜱酷蜱博ㄦ躅汜祆泔铘┅ㄤ彐躅浇酷蜱酷蜱泔铘⒘蝈翳赭狎珲礤铘叛樟鏖翳铒躅殒殂狒轱瞵怩鏖翳溴蝈驽蝈钽轭缈涉箫篚沣邋洚ㄩㄤ弪彐羼踽酷蜱酷蜱博ㄦ躅汜祆泔铘┅ㄤ彐躅溴蝈姝羼踽⒘蝈翳赭狎珲礤铘叛樟鏖翳铒躅殒殂狒轱瞵怩鏖翳溴蝈驽蝈钽轭缈矧ㄥ耢ㄤ弪彐ㄤ弪彐┅ㄡ钿ㄣ镱箴ㄣ镱箴ㄤ弪彐羼踽ㄦ轵篝ㄦ轵篝┅ㄤ弪彐羼踽蝈篝蝈篝┅┅ㄤ彐躅汜祆ㄧ镝泔铘⒃蝙麸痱秭顼犰怡汜祆轭轸ㄤ弪彐顼犰ㄡ痧禊磲脲痱邃殂狒ㄦ轵篝顼犰戾铉翳ㄡ蜱顼犰┅ㄡ痧孱ㄡ蜱顼犰扉篝泔铘┅┅辑矧酷库ㄣ犰酷┅辑矧酷库ㄣ犰库┅辑ㄡ钿酷库ㄣ犰酷ㄣ犰库┅ㄤ彐磲泸鏖翳躅滹忾钿轭珞é怙澌怙澌⒄钿忾钿轭珞徭翦遽汨屮痱弩箝镱轭怙澌屮沐痿翳灬篝ㄩ戾铉翳奖怙澌ㄦ轵篝怙澌啜戾è镬洵趄衢ㄦ殪飙痫轭翦趄衢飒┅ㄦ轵篝怙澌括祜镳骘屮轭蝈篝怙澌泔祆邈Ж躅滹忾钿轭珞镬洵趄衢飑泔祆邈屮皓┅ㄤ彐躅铒舣蝈灬糸镱泔铘⑽彗狒轱怡驷殪躜搴涉秕汜瞌痱秭乾翳孱铒签趄蹂换砰翳弪麽躅滹翳忾钿轭珞鏖翳躅滹忾钿轭珞ㄣ犰殳蝈灬糸镱＇灬礅溽ī蝈趱蝾骝镯铒舣铋飑┅ㄦ躅汜祆泔铘┅ㄤ彐躅忉顼姣ㄥ顼犰蝈篚祠泔铘⑵轭犰箫祯糸镱麸窍撂犷骘遽汨箫祯糸镱泔祆邈翳鲠祯镦咆轭麸翳扉篝遗诱淘换砒馏篚礤暴博畅澡孱换ㄢ徵镦盔盔快浇快ū畅戾è犷篦弪铋飑ㄣ犰殳顼犰＇灬礅溽ī换迈骈怡礓姘ン桢礤箬犁翦泔ㄍ狎棋忪秣轸换镱驳梳惫苟麽溴蝈姝孟匈瘐箬ㄤ弪彐咆屮皓犷篦弪螬┅ㄩㄡ钿铒铛祆犷篦弪螬躅殒蝈篚祠铗弼弪箦犷篦弪螬┅ㄦ躅汜祆泔铘┅┅ㄤ彐躅溴蝈姝泔瘗ㄥ皓⒚镳翳屮痱弩箝镱蝈痨徙轭鲠蜷徕戾鏖翳铄镱弩澡疳螋鏖翳秕鲠蜷徕戾汜忮蝈趱蝾邃狍轶换迈骈怡驷蝰蹊狎犷铒蝣殓辈辈共骑蜱雉麸溴蝈鲠虍篚忪轶磲疸狎＇灬礅溽鲠颟ㄣ镱ㄤ弪彐鲠颟咯┅躅轳蹂骈钿犷麒弪瀛殒＇鲠颦屮皓屮皓ㄤ彐躅箦麸姣ㄥ顼犰蝈篚祠泔铘⑵轭犰躅轳蹂箫祯糸镱麸窍撂犷骘遽汨箫祯糸镱泔祆邈翳鲠祯镦咆轭麸翳扉篝遗诱淘换砒馏篚礤暴博畅澡孱换箦麸盔盔快浇快ū畅戾è犷篦弪铋飑ㄣ犰殳顼犰＇灬礅溽ī瘐箬ㄤ弪彐泔瘗屮皓犷篦弪螬┅ㄩㄡ钿铒铛祆犷篦弪螬躅殒蝈篚祠ㄤ屐弭瀛漉痨殂狒弩犷篦弪呼弩＇溴蝈姝羼踽飑┅ㄦ躅汜祆泔铘┅┅ㄤ彐躅轶鲠屮泔铘换砒犴痨搴ㄩ盔ǐí岿ǐ窥穿┅换向弼孱ㄩ盔岿盔ㄣ镱ㄦ轵篝窥快┅ㄩㄡ钿铒ㄦ轭洵殒犷麒弪＇躅怙躅洵鲠颦屮皓躅殒鲠ㄥ鲠ㄤ弪彐屮屮皓┅ㄦ躅汜祆泔铘┅ㄤ彐躅躅怙躅洵鲠颦ㄥ皓⑸咆犷躅怙躅鲠蚩ㄡ钿鲠颦屮皓铒ㄢ秕钿屮皓┅ㄤ彐躅鲠虔酷蜱泔铘⒂蹉沐邃殒酷蜱轶犷躅轭篝犷糸狒邃鲠蜷徕戾ㄩ躅怙躅洵鲠颦酷蜱暴ㄦ躅汜祆泔铘┅ㄤ彐躅扉箴框弩蹯屮泔铘⒘痧禊ㄦ轵篝屮皓麸蝈篝屮皓犷蝈趱蝾翳蝈篚祠ㄩㄡ钿ㄣ镱箴ㄤ弪彐屮皓躅殒框弩蹯ㄡ痧禊ㄦ轵篝屮皓蝈篝屮皓┅ㄦ躅汜祆泔铘┅ㄤ彐躅蝈疱狒ㄣ镱舂祜镳ㄦ躅汜祆泔铘┅辑ㄩ眶弩眶桢瞟ㄩ眶桢垮祗ㄦ衢飑┅辑ㄩ眶弩眶桢垮祗濠ㄣ犰眶弩舂ㄣ犰眶桢瞟辑ㄩ眶弩眶桢垮祗濠ㄣ犰垮祗濠辑礤礅弪块翦块翦框弩舂┅辑礤礅弪块翦盔框弩舂礤礅弪块翦框弩舂辑戾铉翳ī癌辑戾铉翳盔岿ū款┅戾铉翳岿款┅ㄤ彐躅铛礅弪鸠泔铘麒孱铛礅弪ㄤ弪彐┅ㄦ躅汜祆泔铘┅ㄤ彐躅狒镯泔铘麒孱ㄡ麸ㄤ弪彐┅ㄦ躅汜祆泔铘┅