/*  Part of Extended Libraries for SWI-Prolog

    Author:        Edison Mera
    E-mail:        efmera@gmail.com
    WWW:           https://github.com/edisonm/xlibrary
    Copyright (C): 2021, Process Design Center, Breda, The Netherlands.
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions
    are met:

    1. Redistributions of source code must retain the above copyright
       notice, this list of conditions and the following disclaimer.

    2. Redistributions in binary form must reproduce the above copyright
       notice, this list of conditions and the following disclaimer in
       the documentation and/or other materials provided with the
       distribution.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/

:- module(mapnlist,
          [mapnlist/2,
           mapnlist/3,
           mapnlist/4,
           mapnlist/5,
           mapnlist/6,
           mapnlist/7,
           mapnlist/8
          ]).

:- use_module(library(neck)).
:- use_module(library(lists)).

:- meta_predicate mapnlist(1, ?).
:- meta_predicate mapnlist(2, ?, ?).
:- meta_predicate mapnlist(3, ?, ?, ?).
:- meta_predicate mapnlist(4, ?, ?, ?, ?).
:- meta_predicate mapnlist(5, ?, ?, ?, ?, ?).
:- meta_predicate mapnlist(6, ?, ?, ?, ?, ?, ?).
:- meta_predicate mapnlist(7, ?, ?, ?, ?, ?, ?, ?).

/* Note: declarations can not be autogenerated
Decl :-
    between(1, 7, Length),
    length(LList, Length),
    maplist(=(?), LList),
    Spec =.. [mapnlist, Length|LList],
    Decl = (:- meta_predicate Spec),
    portray_clause(user_error, Decl),
    necki.
*/

Head :-
    between(1, 7, Length),
    length(LList, Length),
    Head =.. [mapnlist, Goal|LList],
    append(LList, [Goal], BodyL),
    Body =.. [mapnlist_|BodyL],
    necki,
    Body.

:- public split_ht/3.

split_ht([H|T], H, T).

Head :-
    between(1, 7, Length),
    length(LList, Length),
    append(LList, [Goal], HeadL),
    Head =.. [mapnlist_|HeadL],
    ( maplist(=([]), LList),
      Body = !
    ; length(HL, Length),
      maplist(split_ht, LList, HL, TL),
      append(HL, [Goal], MapNListHL),
      append(TL, [Goal], MapNListTL),
      MapNListH =.. [mapnlist_|MapNListHL],
      MapNListT =.. [mapnlist_|MapNListTL],
      Body = ( !,
               MapNListH,
               MapNListT
             )
    ; Body =.. [call, Goal|LList]
    ),
    necki,
    Body.
