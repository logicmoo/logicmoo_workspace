/*  Part of XPCE --- The SWI-Prolog GUI toolkit

    Author:        Jan Wielemaker and Anjo Anjewierden
    E-mail:        J.Wielemaker@cs.vu.nl
    WWW:           http://www.swi-prolog.nl/projects/xpce/
    Copyright (c)  2011-2013, University of Amsterdam
                              VU University Amsterdam
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions
    are met:

    1. Redistributions of source code must retain the above copyright
       notice, this list of conditions and the following disclaimer.

    2. Redistributions in binary form must reproduce the above copyright
       notice, this list of conditions and the following disclaimer in
       the documentation and/or other materials provided with the
       distribution.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/

:- module(link_xpce, []).
:- set_prolog_flag(generate_debug_info, true).

/** <module> Associate XPCE with SWI-Prolog

This  file  initialises  XPCE,  the  SWI-Prolog   native  GUI.  XPCE  is
initialised only if it is detected.

The source-location of this file  is packages/xpce/swipl/swipl-rc. It is
installed as <plbase>/<exe-base>.rc, where   <exe-base> is =|swipl-win|=
to associate with the SWI-Prolog gui  application on Windows and =swipl=
on Unix/X11 platforms.
*/

:- multifile
	user:file_search_path/2.

:- dynamic
	pcehomestore_/1.
:- volatile
	pcehomestore_/1.

pcehome_(Home) :-
	pcehomestore_(Home), !.
pcehome_(Home) :-
	(   getenv('XPCEHOME', RawHome)
	;   current_prolog_flag(home, PlHome),
	    (   current_prolog_flag(xpce_version, Version),
		atom_concat('/xpce-', Version, Suffix)
	    ;   Suffix = '/xpce'
	    ),
	    atom_concat(PlHome, Suffix, RawHome)
	),
	exists_directory(RawHome), !,
	absolute_file_name(RawHome, Home),
	asserta(pcehomestore_(Home)).

user:file_search_path(pce, PceHome) :-
	current_prolog_flag(xpce, true),
	pcehome_(PceHome).
user:file_search_path(library, pce('prolog/lib')).
user:file_search_path(foreign, pce(ArchLib)) :-
	current_prolog_flag(arch, Arch),
	atom_concat('lib/', Arch, ArchLib).

% We added a directory to the autoload directories: force reloading the
% index
:- reload_library_index.

gui_setup_ :-
	current_prolog_flag(gui, true), !.
gui_setup_ :-
	(   getenv('DISPLAY', D), D \== ''
	;   current_prolog_flag(windows, true)
	), !,
	create_prolog_flag(gui, true, []),
	menu_setup_,
	editor_setup,
	load_files(user:library(swi_hooks), [silent(true)]).	% help, etc.

menu_setup_ :-					% plwin.exe menus
	current_prolog_flag(console_menu, true),
	load_files(user:library(win_menu), [silent(true)]).
menu_setup_.

editor_setup :-
	current_prolog_flag(editor, default), !,
	set_prolog_flag(editor, pce_emacs).
editor_setup.

:- dynamic
	pce_setup_done/0.

pce_setup_ :-
	pce_setup_done, !.
pce_setup_ :-
	current_prolog_flag(xpce, false), !.
pce_setup_ :-
	pcehome_(PceHome),
	exists_directory(PceHome),
	(   current_prolog_flag(xpce, true)
	->  Preload = true
	;   Preload = false
	),
	set_prolog_flag(xpce, true),
	gui_setup_,
	!,
	asserta(pce_setup_done),
	(   (   Preload == true
	    ;	current_prolog_flag(executable, Executable),
		file_base_name(Executable, Base),
		sub_atom_icasechk(Base, _, pce)
	    )
	->  ensure_loaded(user:library(pce))
	;   true
	).
pce_setup_ :-
	asserta(pce_setup_done).

:- initialization pce_setup_.
